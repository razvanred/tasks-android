import app.sedici.tasks.buildsrc.Android

plugins {
	id("com.android.library")
	id("org.jetbrains.kotlin.android")
	id("org.jetbrains.kotlin.kapt")
}

android {
	compileSdk = Android.compileSdk

	defaultConfig {
		minSdk = Android.minSdk

		testInstrumentationRunner = "app.sedici.tasks.base.android.test.MainTestRunner"

		javaCompileOptions {
			annotationProcessorOptions {
				arguments = [
					"room.schemaLocation": "$projectDir/schemas".toString(),
					"room.incremental"   : "true"
				]
			}
		}
	}

	compileOptions {
		coreLibraryDesugaringEnabled true

		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = "1.8"
	}

	testOptions {
		devices {
			pixel2api29 (com.android.build.api.dsl.ManagedVirtualDevice) {
				device = "Pixel 2"
				apiLevel = 29
				systemImageSource = "aosp"
				abi = "x86"
			}
			nexus5api27 (com.android.build.api.dsl.ManagedVirtualDevice) {
				device = "Nexus 5"
				apiLevel = 27
				systemImageSource = "aosp"
				abi = "x86"
			}
		}
		deviceGroups {
			phone {
				targetDevices.addAll(devices.pixel2api29, devices.nexus5api27)
			}
		}
	}

	packagingOptions {
		exclude "META-INF/AL2.0"
		exclude "META-INF/LGPL2.1"
	}
}

dependencies {
	coreLibraryDesugaring(libs.android.tools.desugar)

	implementation(libs.androidx.room.ktx)
	api(libs.androidx.room.runtime)
	kapt(libs.androidx.room.compiler)

	implementation(libs.dagger.hilt.android)
	kapt(libs.dagger.hilt.compiler)

	api(projects.data.local.common)
	implementation(projects.base.android)

	androidTestImplementation(libs.junit4)
	androidTestImplementation(libs.truth)
	androidTestImplementation(libs.turbine)
	androidTestImplementation(libs.mockk.android)

	androidTestImplementation(libs.androidx.test.core)
	androidTestImplementation(libs.androidx.test.ext.junit)
	androidTestImplementation(libs.androidx.test.rules)
	androidTestImplementation(libs.androidx.test.runner)

	androidTestImplementation(libs.dagger.hilt.android.testing)
	kaptAndroidTest(libs.dagger.hilt.compiler)

	androidTestImplementation(projects.base.android.test)

	androidTestImplementation(projects.data.local.android.testing)
	androidTestImplementation(libs.androidx.arch.core.testing)
}
